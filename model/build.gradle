import io.github.droidkaigi.confsched2021.news.Dep

plugins {
    id("com.android.library")
    id("kotlin-multiplatform")
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
    }

    // delete this when this is fixed https://issuetracker.google.com/issues/175496966
    configurations {
        androidTestApi {}
        androidTestDebugApi {}
        androidTestReleaseApi {}
        testApi {}
        testDebugApi {}
        testReleaseApi {}
    }
}

kotlin {
    android()
    jvm()
    if (System.getProperty("idea.active") == "true") {
        // Fix IntelliJ Unresolved reference
        def onPhone = System.getenv("SDK_NAME")?.startsWith("iphoneos") ?: false
        if (onPhone) {
            iosArm64("ios")
        } else {
            iosX64("ios")
        }
    } else {
        ios()
    }
    sourceSets {
        android
        commonMain.dependencies {
            api project.dependencies.platform(Dep.Coroutines.bom)
            api Dep.Coroutines.core
            api Dep.datetime
        }
        android {}
        all.languageSettings {
            progressiveMode = true
            useExperimentalAnnotation("kotlin.Experimental")
            useExperimentalAnnotation("kotlinx.io.unsafe.ExperimentalTime")
            useExperimentalAnnotation("kotlin.contracts.ExperimentalContracts")
        }
    }
}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
//        languageVersion = "1.5"
//        freeCompilerArgs = ["-Xjvm-enable-preview"]
    }
}
