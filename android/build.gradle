import io.github.droidkaigi.feeder.Dep
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "io.github.droidkaigi.feeder"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile project.file("development.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
        unitTests.all {
            maxHeapSize = "4096m"
            maxParallelForks = Runtime.runtime.availableProcessors() / 2
            testLogging {
                exceptionFormat = "full"
                showCauses true
                showExceptions true
                showStackTraces true
                events = ["failed"]
            }
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig false
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${compose_version}"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies"]
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlin.contracts.ExperimentalContracts"
    }
}

hilt {
    enableExperimentalClasspathAggregation = true
}

dependencies {
    implementation project(':data:repository')
    implementation project(':uicomponent-compose:main')

    // Hilt
    implementation Dep.Dagger.hiltAndroid
    kapt Dep.Dagger.hiltAndroidCompiler

    // For instrumentation tests
    androidTestImplementation Dep.Dagger.hiltAndroidTesting
    kaptAndroidTest Dep.Dagger.hiltAndroidCompiler

    // For local unit tests
    testImplementation Dep.Dagger.hiltAndroidTesting
    kaptTest Dep.Dagger.hiltAndroidCompiler

    implementation('androidx.hilt:hilt-common:1.0.0-alpha03')
    implementation('androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03')
    kapt Dep.Dagger.hiltAndroidCompiler

    implementation Dep.Compose.activity
    implementation Dep.Compose.runtime
    implementation Dep.Compose.ui

    implementation Dep.Kotlin.bom
    implementation Dep.Kotlin.stdlib

    implementation Dep.AndroidX.core
    implementation Dep.AndroidX.fragment
    implementation Dep.AndroidX.activity

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0"
    implementation 'com.google.android.material:material:1.3.0'

    implementation Dep.FirebaseCrashlytics.android

    implementation Dep.firebaseAuth
    implementation Dep.firebaseMessaging
    implementation Dep.Jetpack.startup

    // For Layout inspector
    debugImplementation Dep.Compose.tooling
    debugImplementation Dep.Kotlin.reflect

    implementation Dep.arbor

    testImplementation 'junit:junit:4.13.1'
    testImplementation Dep.Coroutines.test
    testImplementation 'io.kotest:kotest-assertions-core:4.3.2'
    // https://github.com/cashapp/turbine/issues/10
    testImplementation 'app.cash.turbine:turbine:0.2.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

