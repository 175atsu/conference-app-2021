import io.github.droidkaigi.confsched2021.news.Dep
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "io.github.droidkaigi.confsched2021.news"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
        unitTests.all {
            maxHeapSize = "4096m"
            maxParallelForks = Runtime.runtime.availableProcessors() / 2
            testLogging {
                exceptionFormat = "full"
                showCauses true
                showExceptions true
                showStackTraces true
                events = ["failed"]
            }
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${compose_version}"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies"]
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
        freeCompilerArgs += "-Xopt-in=kotlin.contracts.ExperimentalContracts"
    }
}

dependencies {
    implementation project(":data")
    implementation project(':uicomponent-compose:main')

    // Hilt
    implementation Dep.Dagger.hiltAndroid
    kapt Dep.Dagger.hiltAndroidCompiler

    // For instrumentation tests
    androidTestImplementation Dep.Dagger.hiltAndroidTesting
    kaptAndroidTest Dep.Dagger.hiltAndroidCompiler

    // For local unit tests
    testImplementation Dep.Dagger.hiltAndroidTesting
    kaptTest Dep.Dagger.hiltAndroidCompiler

    implementation('androidx.hilt:hilt-common:1.0.0-alpha02')
    implementation('androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02')
    kapt Dep.Dagger.hiltAndroidCompiler

    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"

    implementation Dep.Kotlin.bom
    implementation Dep.Kotlin.stdlib
    implementation 'androidx.core:core-ktx:1.5.0-alpha05'
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "androidx.activity:activity-ktx:1.1.0"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation 'com.google.android.material:material:1.2.1'

    implementation Dep.firebaseAuth
    implementation Dep.Jetpack.startup

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.1'
    testImplementation 'io.kotest:kotest-assertions-core:4.3.1'
    // https://github.com/cashapp/turbine/issues/10
    testImplementation 'app.cash.turbine:turbine:0.2.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

apply plugin: 'com.google.gms.google-services'
